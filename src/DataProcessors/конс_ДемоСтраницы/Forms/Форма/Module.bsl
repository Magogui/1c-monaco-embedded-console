
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	конс_ОбщийМодульСервер.ПриСозданииНаСервере(ЭтотОбъект);
	
	СоответствияКодовНаСтранице = конс_ЗаполнитьСоответствияРазделовКода();

	ЭтотОбъект.конс_СоответствияКодовНаСтранице	= СоответствияКодовНаСтранице;
	ЭтотОбъект.конс_КлючАктивнойСтраницы		= "ДекорацияПерваяСтраница";

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	конс_ОбщийМодульКлиент.ПриОткрытии(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("конс_НачальноеЗаполнение", 2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	конс_ОбщийМодульКлиент.ПередЗаписью(ЭтотОбъект, Отказ, ПараметрыЗаписи);
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	конс_СохранитьКодИзКонсоли(ТекущийОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	конс_ОбщийМодульКлиент.ПередЗакрытием(ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовПараметрыЗапроса

&НаКлиенте
Процедура конс_ПараметрыЗапросаКодЗначениеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ПараметрыЗапроса.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", ТекущиеДанные.Имя);
	ПараметрыФормы.Вставить("Код", ТекущиеДанные.КодЗначение);
	ПараметрыФормы.Вставить("конс_HTML", конс_HTML);
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("конс_КодДо", конс_ОбщийМодульКлиент.ПолучитьТекст(ЭтотОбъект));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("конс_ПравилоЗаполненияЗавершение", ЭтотОбъект, ДопПараметры);
	РежимОткрытия = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	ОткрытьФорму("ОбщаяФорма.конс_РедактированиеКода", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор, , , ОписаниеОповещения, РежимОткрытия);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура конс_ВкладкаНажатие(АктивированнаяВкладка)
	
	Для Каждого Декорация Из Элементы.ГруппаЭмуляцияВкладок.ПодчиненныеЭлементы Цикл
		Декорация.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	КонецЦикла;

	АктивированнаяВкладка.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.Подчеркивание, 2);
	
	Для Каждого Страница Из Элементы.ГруппыВкладок.ПодчиненныеЭлементы Цикл
		Если ТипЗнч(Страница) = Тип("ГруппаФормы")
			И Не Страница = Элементы.конс_ГруппаКонсоли Тогда
			Страница.Видимость = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если АктивированнаяВкладка.Имя = "ДекорацияПерваяСтраница" Тогда
		Элементы.СтраницаПервая.Видимость = Истина;
	ИначеЕсли АктивированнаяВкладка.Имя = "ДекорацияВтораяСтраница" Тогда
		Элементы.СтраницаВторая.Видимость = Истина;
	КонецЕсли;
	
	ТекущийЭлемент = Элементы.ДекорацияПустышка;
	
	ЭтотОбъект.конс_КлючАктивированнойСтраницы = АктивированнаяВкладка.Имя;
	
	конс_ПриСменеСтраницы();
	
КонецПроцедуры

&НаКлиенте
Процедура конс_ЗаполнитьПараметрыЗапроса(Команда)
	
	ТекстЗапроса = конс_ОбщийМодульКлиент.ПолучитьТекст(ЭтотОбъект);
	конс_ЗаполнитьПараметрыЗапросаНаСервере(ТекстЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура конс_НачальноеЗаполнение() Экспорт
	
	конс_ОбщийМодульКлиент.View(ЭтотОбъект).clearMetadata();
	конс_ОбновитьСписокОбщихМодулей();
	
	КодПервойСтраницы = ЭтотОбъект.конс_СоответствияКодовНаСтранице[ЭтотОбъект.конс_КлючАктивнойСтраницы];
	конс_ОбщийМодульКлиент.УстановитьТекст(ЭтотОбъект, КодПервойСтраницы.Код, Неопределено, Ложь);
	
	Доступность = ЭтотОбъект.конс_ДокументЗагружен;
	
КонецПроцедуры

&НаКлиенте
Процедура конс_ОбновитьСписокОбщихМодулей()
	
	КоллекцияМетаданных = ПолучитьИзВременногоХранилища(ЭтотОбъект.конс_АдресОбщихМодулей);
	
	Если КоллекцияМетаданных <> Неопределено Тогда
		Результат = конс_ОбщийМодульКлиент.View(ЭтотОбъект).updateMetadata(КоллекцияМетаданных, "commonModules.items");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура конс_ПодключаемыйДокументСформирован(Элемент)
	
	Если ЭтотОбъект.конс_ИсходникиЗагружены Тогда
		конс_ОбщийМодульКлиент.ДокументСформирован(Элемент, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура конс_ПодключаемыйПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Если НЕ ЭтотОбъект.конс_ТолькоДляЧтения Тогда
		конс_ОбщийМодульКлиент.ОбработатьСобытиеРедактора(ЭтотОбъект, ДанныеСобытия.Event.eventData1C);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура конс_КонструкторЗапроса(Команда)
	
	конс_ОбщийМодульКлиент.ВызватьКонструкторЗапроса(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура конс_ТолькоДляЧтения(Команда)
	
	конс_ОбщийМодульКлиент.ТолькоДляЧтения(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура конс_БыстрыеПодсказки(Команда)
	
	конс_ОбщийМодульКлиент.БыстрыеПодсказки(ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура конс_ПодсвечиватьЗапросы(Команда)
	
	конс_ОбщийМодульКлиент.ПодсвечиватьЗапросы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура конс_СтрокаСостояния(Команда)
	
	конс_ОбщийМодульКлиент.СтрокаСостояния(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура конс_УстановитьВидимость(ЕстьКодНаСтранице)
	
	КлючСтраницы = ЭтотОбъект.конс_КлючАктивированнойСтраницы;
	
	ВключитьДоступность = Ложь;
	Если ЕстьКодНаСтранице Тогда
		ВключитьДоступность = Истина;
	КонецЕсли;
	Элементы.конс_ГруппаКонсоли.Доступность = ВключитьДоступность;
	
	ВключитьРежимЗапроса =  ЭтотОбъект.конс_КлючАктивированнойСтраницы = "ДекорацияВтораяСтраница";
	
	ТекстСообщения = СтрШаблон("Режим запроса при смене страницы: %1",
		Формат(ВключитьРежимЗапроса, "БЛ=отключен; БИ=включен;") );
	
	Сообщить(ТекстСообщения);
	
	конс_ОбщийМодульКлиент.ПереключитьРежимЗапроса(ЭтотОбъект, ВключитьРежимЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура конс_ПриСменеСтраницы()
	
	ЕстьКодНаСтранице = конс_ОбщийМодульКлиент.ПриИзмененииВкладкиСКодом(ЭтотОбъект);
	
	конс_УстановитьВидимость(ЕстьКодНаСтранице);
	
КонецПроцедуры

&НаСервере
Функция конс_ЗаполнитьСоответствияРазделовКода()
	
	СоответствияКодовНаСтранице = Новый Структура;
	
	СоответствиеРеквизитов = Новый Структура;
	СоответствиеРеквизитов.Вставить("Реквизит", "КодПервойСтраницы"); 
	СоответствиеРеквизитов.Вставить("Код", Объект.КодПервойСтраницы);
	СоответствияКодовНаСтранице.Вставить("ДекорацияПерваяСтраница", СоответствиеРеквизитов);
	
	СоответствиеРеквизитов = Новый Структура;
	СоответствиеРеквизитов.Вставить("Реквизит", "КодПервойСтраницы"); 
	СоответствиеРеквизитов.Вставить("Код", Объект.КодВторойСтраницы);
	СоответствияКодовНаСтранице.Вставить("ДекорацияВтораяСтраница", СоответствиеРеквизитов);
	
	Возврат СоответствияКодовНаСтранице;

КонецФункции

&НаСервере
Процедура конс_СохранитьКодИзКонсоли(ТекущийОбъект)
	
	Для Каждого Страница Из ЭтотОбъект.конс_СоответствияКодовНаСтранице Цикл
		СтруктураХранения = Страница.Значение;
		ТекущийОбъект[СтруктураХранения.Реквизит] = СтруктураХранения.Код;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура конс_ЗаполнитьПараметрыЗапросаНаСервере(Знач ТекстЗапроса)
	
	Схема = Новый СхемаЗапроса;
	Схема.УстановитьТекстЗапроса(ТекстЗапроса);
	ВремПараметрыЗапроса = Схема.НайтиПараметры();
	Для Каждого ПараметрЗапроса Из ВремПараметрыЗапроса Цикл
		Если Объект.ПараметрыЗапроса.НайтиСтроки(Новый Структура("Имя", ПараметрЗапроса.Имя)).Количество() = 0 Тогда
			НоваяСтрока = Объект.ПараметрыЗапроса.Добавить();
			НоваяСтрока.Имя = ПараметрЗапроса.Имя;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура конс_ПравилоЗаполненияЗавершение(Результат, ДопПараметры) Экспорт
	
	конс_ОбщийМодульКлиент.ОчиститьТекст(ЭтотОбъект);
	конс_ОбщийМодульКлиент.УстановитьТекст(ЭтотОбъект, ДопПараметры.конс_КодДо, Неопределено, Ложь);
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ПараметрыЗапроса.ТекущиеДанные;
	ТекущиеДанные.КодЗначение = Результат;
	
КонецПроцедуры

#КонецОбласти